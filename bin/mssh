#!/usr/bin/perl

# Created on: 2012-01-18 08:29:06
# Create by:  Ivan Wills
# $Id$
# $Revision$, $HeadURL$, $Date$
# $Revision$, $Source$, $Date$

use strict;
use warnings;
use version;
use Scalar::Util;
use List::Util;
#use List::MoreUtils;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper qw/Dumper/;
use English qw/ -no_match_vars /;
use FindBin qw/$Bin/;
use Path::Class;
use POSIX ":sys_wait_h";

our $VERSION = version->new('0.0.1');
my ($name)   = $PROGRAM_NAME =~ m{^.*/(.*?)$}mxs;

my %option = (
    host    => [],
    verbose => 0,
    man     => 0,
    help    => 0,
    VERSION => 0,
);

if ( !@ARGV ) {
    pod2usage( -verbose => 1 );
}

main();
exit 0;

sub main {


    Getopt::Long::Configure('bundling');
    GetOptions(
        \%option,
        'host|h=s@',
        'parallel|p=i',
        'test|t!',
        'verbose|v+',
        'man',
        'help',
        'VERSION!',
    ) or pod2usage(2);

    if ( $option{'VERSION'} ) {
        print "$name Version = $VERSION\n";
        exit 1;
    }
    elsif ( $option{'man'} ) {
        pod2usage( -verbose => 2 );
    }
    elsif ( $option{'help'} ) {
        pod2usage( -verbose => 1 );
    }
    elsif ( !@{ $option{host} } ) {
        warn "You must specify at least one host!\n";
        pod2usage( -verbose => 1 );
    }

    # do stuff here
    my $int_re       = qr/ [0-9a-zA-Z] /xms;
    my $range_re     = qr/ ($int_re) [.][.] ($int_re)   /xms;
    my $group_re     = qr/ (?: $int_re | $range_re )    /xms;
    my $seperated_re = qr/ $group_re (?: , $group_re )* /xms;
    my $num_range_re = qr/ \[ ( $seperated_re ) \]      /xms;
    my @hosts;

    while ( my $host_range = shift @{$option{host} } ) {
        my ($num_range) = $host_range =~ /$num_range_re/;

        if (!$num_range) {
            push @hosts, $host_range;
            next;
        }

        my @numbs    = map { /$range_re/ ? ($1 .. $2) : ($_) } split /,/, $num_range;
        my @hostmaps = map { $a=$host_range; $a =~ s/$num_range_re/$_/e; $a } @numbs;

        if ( $hostmaps[0] =~ /$num_range_re/ ) {
            push @{$option{host}}, @hostmaps;
        }
        else {
            push @hosts, @hostmaps;
        }
    }

    print join "\n", @hosts, '' if $option{verbose} > 1 || !@ARGV;

    return if !@ARGV;

    my $remote_cmd = shift @ARGV;
    my @children;

    for my $host (@hosts) {
        my $cmd = "ssh $host '$remote_cmd'";
        print "-- $cmd\n" if ( $option{verbose} || $option{test} ) && !$option{parallel};
        next if $option{test};

        if ( $option{parallel} ) {
            my $child = fork;

            if ( $child ) {
                # parent stuff
                push @children, $child;

                if ( @children == $option{parallel} ) {
                    while ( my $pid = shift @children ) {
                        waitpid $pid, 0;
                    }
                }
            }
            elsif ( defined $child ) {
                # child code
                my $out = `$cmd 2>&1`;

                print "\n$cmd\n";
                print $out;
                exit;
            }
            else {
                die "Error: $!\n";
            }
        }
        else {
            system $cmd;
        }
    }

    wait;

    return;
}

__DATA__

=head1 NAME

mssh - Multi host ssh executer

=head1 VERSION

This documentation refers to mssh version 0.1.

=head1 SYNOPSIS

   mssh [ --VERSION | --help | --man ]
   mssh -h host [ -v | --verbose ] 'remote command'

 OPTIONS:
  -h --host[=]str Host or range of hosts to execute 'remote command' on
                  The range must be sourounded by [] and can be either comma
                  seperated integer/letters or .. for a range of all values
                  from the first to last.
                  EG [0..5] for 1,2,3,4 and 5
                     [0,5]      1 and 5
                     [0..3,5    1,2,3 and 5
  -p --parallel[=]int
                  Fork calls to each server to run in parallel the value limits
                  the number of processes called at once

  -v --verbose    Show more detailed option
     --version    Prints the version information
     --help       Prints this help information
     --man        Prints the full documentation for mssh



=head1 DESCRIPTION

A full description of the module and its features.

May include numerous subsections (i.e., =head2, =head3, etc.).


=head1 SUBROUTINES/METHODS

A separate section listing the public components of the module's interface.

These normally consist of either subroutines that may be exported, or methods
that may be called on objects belonging to the classes that the module
provides.

Name the section accordingly.

In an object-oriented module, this section should begin with a sentence (of the
form "An object of this class represents ...") to give the reader a high-level
context to help them understand the methods that are subsequently described.

=head1 DIAGNOSTICS

A list of every error and warning message that the module can generate (even
the ones that will "never happen"), with a full explanation of each problem,
one or more likely causes, and any suggested remedies.

=head1 CONFIGURATION AND ENVIRONMENT

A full explanation of any configuration system(s) used by the module, including
the names and locations of any configuration files, and the meaning of any
environment variables or properties that can be set. These descriptions must
also include details of any configuration language used.

=head1 DEPENDENCIES

A list of all of the other modules that this module relies upon, including any
restrictions on versions, and an indication of whether these required modules
are part of the standard Perl distribution, part of the module's distribution,
or must be installed separately.

=head1 INCOMPATIBILITIES

A list of any modules that this module cannot be used in conjunction with.
This may be due to name conflicts in the interface, or competition for system
or program resources, or due to internal limitations of Perl (for example, many
modules that use source code filters are mutually incompatible).

=head1 BUGS AND LIMITATIONS

A list of known problems with the module, together with some indication of
whether they are likely to be fixed in an upcoming release.

Also, a list of restrictions on the features the module does provide: data types
that cannot be handled, performance issues and the circumstances in which they
may arise, practical limitations on the size of data sets, special cases that
are not (yet) handled, etc.

The initial template usually just has:

There are no known bugs in this module.

Please report problems to Ivan Wills (ivan.wills@gmail.com).

Patches are welcome.

=head1 AUTHOR

Ivan Wills - (ivan.wills@gmail.com)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2012 Optus (1 Lyon Park Rd, Macquarie Park, NSW, Australia).
All rights reserved.

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L<perlartistic>.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.

=cut
