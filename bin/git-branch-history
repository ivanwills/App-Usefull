#!/usr/bin/perl

# Created on: 2018-05-10 07:48:34
# Create by:  Ivan Wills
# $Id$
# $Revision$, $HeadURL$, $Date$
# $Revision$, $Source$, $Date$

use strict;
use warnings;
use version;
use Scalar::Util;
use List::Util;
#use List::MoreUtils;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper qw/Dumper/;
use English qw/ -no_match_vars /;
use FindBin qw/$Bin/;
use Path::Tiny;

our $VERSION = version->new('0.0.1');
my ($name)   = $PROGRAM_NAME =~ m{^.*/(.*?)$}mxs;

my %option = (
    number  => 10,
    verbose => 0,
    man     => 0,
    help    => 0,
    VERSION => 0,
);

main();
exit 0;

sub main {
    Getopt::Long::Configure('bundling');
    GetOptions(
        \%option,
        'number|n=i',
        'uniq|u!',
        'quiet|q',
        'checkout|c=i',
        'purge|p',
        'test|t!',
        'verbose|v+',
        'man',
        'help',
        'VERSION!',
    ) or pod2usage(2);

    if ( $option{'VERSION'} ) {
        print "$name Version = $VERSION\n";
        exit 1;
    }
    elsif ( $option{'man'} ) {
        pod2usage( -verbose => 2 );
    }
    elsif ( $option{'help'} ) {
        pod2usage( -verbose => 1 );
    }

    # do stuff here
    return history();
}

sub history {
    my $root = `git rev-parse --show-toplevel`;
    chomp $root;
    my $log_file = "$root/.git/branch.log";
    open my $fh, '<', $log_file or die "Could not open '$log_file' : $!\n";
    my @history;
    my %seen = (
        '=' => {
            count => 0,
            desc  => 'Both local and remove removed (use -p to clean)',
        },
        '-' => {
            count => 0,
            desc  => 'Local branch exists but not remove (either it has been deleted or you haven\'t pushed it yet)',
        },
        '*' => {
            count => 0,
            desc  => 'Remote branch exsits but there is no local copy',
        },
    );

    while (my $history = <$fh>) {
        chomp $history;
        my ($date, $time, $branch) = split /\s/, $history, 3;
        push @history, $option{quiet} ? [ $branch ] : [ $date, $time, $branch ];
    }

    if ($option{uniq}) {
        my @uniq;
        my %branches;
        while (@uniq < $option{number} && @history) {
            my $item = pop @history;
            if ( $branches{$item->[2]}++ ) {
                next;
            }
            unshift @uniq, $item;
        }
        @history = @uniq;
    }

    # don't try to show more history than exists
    $option{number} = $#history if $option{number} > $#history;

    @history = @history[ $#history + 1 - $option{number} .. $#history ];

    if ( $option{verbose} ) {
        system qw/git remote prune origin/;
        my $purged = 0;
        my $log_temp = "$log_file.temp";
        path($log_file)->copy($log_temp);
        for my $branch (@history) {
            # check if the upstream is removed
            my @local = `git log -n1 $branch->[2] 2> /dev/null`;
            my @remote = `git log -n1 origin/$branch->[2] 2> /dev/null`;
            if (!@local && !@remote) {
                push @$branch, '=';
                $seen{'='}{count}++;
                if ( $option{purge} ) {
                    $purged++;
                    warn "grep -vE ' $branch->[2]\$' $log_temp > $log_file\n";
                    `grep -vE ' $branch->[2]\$' $log_temp > $log_file`;
                    path($log_file)->copy($log_temp);
                }
            }
            elsif (!@remote) {
                push @$branch, '-';
                $seen{'-'}{count}++;
            }
            elsif (!@local) {
                push @$branch, '*';
                $seen{'*'}{count}++;
            }

        }

        if ($purged) {
            unlink $log_temp;
            warn "Purged $purged branches for the branch history\n";
            return history();
        }
    }

    my $i = $option{number};
    print join "\n", (
        map {join ' ', (defined $option{verbose} ? ('[' . $i-- .']'): ()), @$_}
        @history), '';

    if ( $option{verbose} ) {
        for my $key (keys %seen) {
            if ($seen{$key}{count}) {
                print "$key $seen{$key}{desc}\n";
            }
        }
    }

    if ( $option{checkout} ) {
        warn "\nChecking out $history[-$option{checkout}][2]\n";
        if (!$option{test}) {
            system "git co $history[-$option{checkout}][2]";
        }
    }
    return;
}

__DATA__

=head1 NAME

git-branch-history - Show the history of branch changes saved by git co

=head1 VERSION

This documentation refers to git-branch-history version 0.0.1

=head1 SYNOPSIS

   git-branch-history [option]

 OPTIONS:
  -n --number[=]int
                Limit shown history to this number of branches
  -u --uniq     Only show a unique history of branches
  -q --quiet    Hide non-branch details
  -c --checkout[=]int
                Checkout the branch this far back in history
  -p --purge    Remove branches from history log that have been both locally
                and remotely deleted. Note this is a permanent action.

  -v --verbose  Show more detailed information about branches
     --version  Prints the version information
     --help     Prints this help information
     --man      Prints the full documentation for git-branch-history

=head1 DESCRIPTION

=head1 SUBROUTINES/METHODS

=head1 DIAGNOSTICS

=head1 CONFIGURATION AND ENVIRONMENT

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

=head1 BUGS AND LIMITATIONS

There are no known bugs in this module.

Please report problems to Ivan Wills (ivan.wills@gmail.com).

Patches are welcome.

=head1 AUTHOR

Ivan Wills - (ivan.wills@gmail.com)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2018 Ivan Wills (14 Mullion Close, Hornsby Heights, NSW Australia 2077).
All rights reserved.

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L<perlartistic>.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.

=cut
