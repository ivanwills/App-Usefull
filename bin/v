#!/usr/bin/env perl

# Created on: 2009-07-15 12:54:35
# Create by:  Ivan Wills
# $Id$
# $Revision$, $HeadURL$, $Date$
# $Revision$, $Source$, $Date$

use strict;
use warnings;
use version;
use List::MoreUtils qw/uniq/;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper qw/Dumper/;
use English qw/ -no_match_vars /;
use FindBin qw/$Bin/;
use File::Find;
use Path::Class;
use File::CodeSearch::Files;

our $VERSION = version->new('0.0.1');
my ($name)   = $PROGRAM_NAME =~ m{^.*/(.*?)$}mxs;

my %option = (
    schema  => 'lib/MyApp/DB',
    max     => 12,
    verbose => 0,
    man     => 0,
    help    => 0,
    VERSION => 0,
);

main();
exit 0;

sub main {

    Getopt::Long::Configure('bundling');
    GetOptions(
        \%option,
        'schema|s=s',
        'max|m=i',
        'test|t!',
        'tags|T!',
        'verbose|v+',
        'man',
        'help',
        'VERSION!',
    ) or pod2usage(2);

    if ( $option{'VERSION'} ) {
        print "$name Version = $VERSION\n";
        exit 1;
    }
    elsif ( $option{'man'} ) {
        pod2usage( -verbose => 2 );
    }
    elsif ( $option{'help'} ) {
        pod2usage( -verbose => 1 );
    }

    # do stuff here
    my $type = shift @ARGV || 'perl';
    create_tags();

    my @files =
          $type eq 'perl'      ? open_perl()
        : $type eq 'test'      ? open_tests()
        : $type eq 'tests'     ? open_tests()
        : $type eq 'templates' ? open_templates()
        : $type eq 'static'    ? open_templates()
        : $type eq 'db'        ? open_database()
        : $type eq 'all'       ? ( open_perl(), open_templates(), open_database() )
        :                        ($type, @ARGV);

    die "Could not find any files!\n" if !@files;

    warn        'vim \'' . (join "' '", uniq sort @files) . "'" if $option{verbose} || $option{test};
    return exec 'vim \'' . (join "' '", uniq sort @files) . "'" if !$option{test};
}

sub open_perl {
    my @files;
    my %dirs = (
        bin     => { order => 1, extension => 0 },
        lib     => { order => 2, extension => 1 },
        example => { order => 5, extension => 0 },
        eg      => { order => 6, extension => 0 },
        helper  => { order => 7, extension => 0 },
        t       => { order => 3, extension => 1 },
        tx      => { order => 4, extension => 1 },
    );
    my $file_test = File::CodeSearch::Files->new( include_type => [qw/perl test/] );

    for my $dir ( sort { $dirs{$a}{order} <=> $dirs{$b}{order} } keys %dirs ) {
        warn "Dir $dir\n" if $option{verbose} > 1;
        next if !-d $dir;
        last if @files > $option{max};

        my @children = dir($dir)->children;

        while (my $file = shift @children) {
            print {*STDERR} "  Found $file" if $option{verbose} > 2;
            if (-d $file) {
                if ( $file =~ m{ /.(?: svn | bzr | git ) | /CVS }xms ) {
                    print {*STDERR} "\n" if $option{verbose} > 2;
                    next;
                }

                push @children, $file->children;
            }
            else {
                if ( !$file_test->file_ok($file) ) {
                    print {*STDERR} "\n" if $option{verbose} > 2;
                    next;
                }
                #next if $dirs{$dir}{extension} && $file !~ /[.] (?: pm | pl | pod | t ) $/xms;
                #next if $file =~ m{[.]sw[po]$};
                #next if -b $file;

                push @files, $file;
                warn "$file\n" if $option{verbose} && $option{verbose} <= 2;
                print {*STDERR} " OK" if $option{verbose} > 2;
            }
            print {*STDERR} "\n" if $option{verbose} > 2;
        }
    }

    return @files;
}

sub open_tests {
    my @files;
    my %dirs = (
        t       => { order => 3, extension => 1 },
        tx      => { order => 4, extension => 1 },
    );

    for my $dir ( sort { $dirs{$a}{order} <=> $dirs{$b}{order} } keys %dirs ) {
        next if !-d $dir;
        last if @files > $option{max};

        my @children = dir($dir)->children;

        while (my $file = shift @children) {
            warn "Found $file\n" if $option{verbose} > 1;
            if (-d $file) {
                next if $file =~ m{ /.(?: svn | bzr | git ) | /CSV }xms;

                push @children, $file->children;
            }
            else {
                next if $dirs{$dir}{extension} && $file !~ /[.] (?: pm | pl | pod | t ) $/xms;
                next if $file =~ m{[.]sw[po]$};
                next if -b $file;

                push @files, $file;
                warn "$file\n" if $option{verbose};
            }
        }
    }

    return @files;
}

sub open_templates {
    my @files;

    for my $dir (qw{template templates root html data/templates js css}) {
        next if !-d $dir;
        warn $dir;
        my @children = dir($dir)->children;

        FILE:
        while (my $file = shift @children) {
            if (-d $file) {
                next if $file =~ /(?: [.] (?: svn | git | bzr ) | CVS ) $/xms;
                push @children, $file->children;
            }
            elsif (-B $file) {
                next FILE;
            }
            else {
                next if $file =~ m{ [.] (?: sw[po] | png | gif | jpe?g ) $}xms;
                next if $file =~ /jquery/;
                return if -b $file;
                push @files, $file;
            }
        }
    }

    return @files;
}

sub open_database {
    my @files;

    for my $dir ('db', $option{schema}) {
        find(
            sub {
                return if -d $_ || !/[.] (?: pm | sql | plsql ) $/xms;

                my ($file) = $File::Find::name;
                $file =~ s{^(.*)/$dir}{$dir}xms;

                push @files, $file;
            },
            $dir
        );
    }

    return @files;
}

sub create_tags {
    my $tag = file('/usr', 'bin', 'ctags');
    if ( $option{tags} && -x $tag ) {
        my @exclude = qw/blib _build Build tmp/;
        my $cmd = "$tag -R --exclude=" . join ' --exclude=', @exclude;
        warn "$cmd\n" if $option{verbose} || $option{test};
        system $cmd if !$option{test};
    }
}

__DATA__

=head1 NAME

v - Opens files in vim baised on type and standard perl project directory hirachy

=head1 VERSION

This documentation refers to v version 0.1.

=head1 SYNOPSIS

   v [option] [perl|templates|db|all]

 OPTIONS:
  perl      - Opens all .pl, .pm, .pod and .t fiels in the lib, bin and t
              directores. This is the default action
  test      - Opens all test files (*.t)
  templates - Opens all files in templates?, root, html data/templates directors
  db        - Opens all files in the db directory
  all       - Open all of the above files

  -m --max=int       The maximum number of files to open before stopping
                     opening files for other directories

  -v --verbose       Show more detailed option
     --version       Prints the version information
     --help          Prints this help information
     --man           Prints the full documentation for v

=head1 DESCRIPTION

A full description of the module and its features.

May include numerous subsections (i.e., =head2, =head3, etc.).


=head1 SUBROUTINES/METHODS

A separate section listing the public components of the module's interface.

These normally consist of either subroutines that may be exported, or methods
that may be called on objects belonging to the classes that the module
provides.

Name the section accordingly.

In an object-oriented module, this section should begin with a sentence (of the
form "An object of this class represents ...") to give the reader a high-level
context to help them understand the methods that are subsequently described.

=head1 DIAGNOSTICS

A list of every error and warning message that the module can generate (even
the ones that will "never happen"), with a full explanation of each problem,
one or more likely causes, and any suggested remedies.

=head1 CONFIGURATION AND ENVIRONMENT

A full explanation of any configuration system(s) used by the module, including
the names and locations of any configuration files, and the meaning of any
environment variables or properties that can be set. These descriptions must
also include details of any configuration language used.

=head1 DEPENDENCIES

A list of all of the other modules that this module relies upon, including any
restrictions on versions, and an indication of whether these required modules
are part of the standard Perl distribution, part of the module's distribution,
or must be installed separately.

=head1 INCOMPATIBILITIES

A list of any modules that this module cannot be used in conjunction with.
This may be due to name conflicts in the interface, or competition for system
or program resources, or due to internal limitations of Perl (for example, many
modules that use source code filters are mutually incompatible).

=head1 BUGS AND LIMITATIONS

A list of known problems with the module, together with some indication of
whether they are likely to be fixed in an upcoming release.

Also, a list of restrictions on the features the module does provide: data types
that cannot be handled, performance issues and the circumstances in which they
may arise, practical limitations on the size of data sets, special cases that
are not (yet) handled, etc.

The initial template usually just has:

There are no known bugs in this module.

Please report problems to Ivan Wills (ivan.wills@gmail.com).

Patches are welcome.

=head1 AUTHOR

Ivan Wills - (ivan.wills@gmail.com)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2009 Ivan Wills (14 Mullion Close, Hornsby Heights, NSW Australia 2077).
All rights reserved.

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L<perlartistic>.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.

=cut
