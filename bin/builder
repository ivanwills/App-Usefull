#!/usr/bin/env perl

# Created on: 2009-04-22 14:31:10
# Create by:
# $Id$
# $Revision$, $HeadURL$, $Date$
# $Revision$, $Source$, $Date$

use strict;
use warnings;
use version;
use Carp qw/carp croak cluck confess longmess/;
use Getopt::Alt qw/get_options/;
use Pod::Usage;
use Data::Dumper qw/Dumper/;
use English qw/ -no_match_vars /;
use FindBin qw/$Bin/;
use POSIX qw/strftime/;
use VCS::Which;
use Path::Tiny;
use File::Copy qw/copy/;
use Config::General;
use DateTime;
use YAML::XS qw/LoadFile/;
use CPAN::Uploader;

our $VERSION = version->new('0.2.0');
my ($name)   = $PROGRAM_NAME =~ m{^.*/(.*?)$}mxs;

my $opt;
my %alt_opt = (
    "dist-clean"   => 'clean',
    c              => 'clean',
    cl             => 'clean',
    "author-tests" => 'author',
    a              => 'author',
    au             => 'author',
    i              => 'install',
    in             => 'install',
    I              => 'lib',
    t              => 'tags',
    f              => 'force',
    "pre-commands" => 'pre_commands',
    p              => 'pre_commands',
    v              => 'verbose',
);

if (!@ARGV) {
    pod2usage( -verbose => 1 );
}

main();
exit 0;

sub main {

    my %default;
    my $config;
    if ( -e '.builderrc' ) {
        $config = Config::General->new('.builderrc');
        %default = ( %default, $config->getall );
        for my $key ( keys %default ) {
            if ( $alt_opt{$key} ) {
                $default{$alt_opt{$key}} = $default{$key};
                delete $default{$key};
            }
        }
        if ( $default{pre_commands} && ref $default{pre_commands} eq 'HASH' ) {
            $default{pre_commands} = [
                map {"$_ $default{pre_commands}{$_}"} keys %{ $default{pre_commands} }
            ];
        }
    }

    my %base = (
        install  => 'sudo ',
        perl     => $EXECUTABLE_NAME,
        parallel => 2,
        travis_min => '5.01',
    );
    $opt = get_options(
        {
            bundle  => 1,
            help    => 1,
            default => {
                %base,
                %default,
            },
        },
        [
            'clean|dist-clean|c',
            'author|author-tests|a!',
            'parallel|parallel-tests|j=i',
            'install|i=s',
            'lib|I=s@',
            'tags|t!',
            'force|f+',
            'do|d',
            'brews|b',
            'pre_commands|pre-commands|p=s@',
            'perl=s',
            'brew=s',
            'travis_min|travis-min|tmin=f',
            'travis_max|travis-max|tmax=f',
            'save',
            'verbose|v+',
        ]
    );
    my $level = shift @ARGV || '';

    # Determine weather the package is using Module::Build or make files

    if ( $opt->save ) {
        my %values = %$opt;
        for my $key (keys %base) {
            delete $values{$key} if $values{$key} eq $base{$key};
        }
        delete $values{save};
        $config ||= Config::General->new;
        $config->save_file('.builderrc', \%values);
    }

    if ( ( $opt->clean || $level eq 'release' || $level eq 'dist' ) && built() ) {
        builder('distclean');
        system q{find blib 2>/dev/null | grep '[.][^/]+[.]sw[poxnm]$' | xargs rm};
        return if !$level;
    }

    if ( !built() ) {
        create_tags();
        system $opt->perl . ' ' . build_pl();
    }
    else {
        builder('clean');
    }

    system builder();

    $ENV{TEST_AUTHOR}     = $opt->author;
    $ENV{RELEASE_TESTING} = $opt->author;
    $ENV{HARNESS_OPTIONS} = 'j' . $opt->parallel;
    $ENV{PERL5LIB}        = "inc:$ENV{PERL5LIB}";

    if ( $level eq 'dist' ) {
        # turn on author tests before making a distribution
        $ENV{TEST_AUTHOR}     = defined $ENV{TEST_AUTHOR}     ? $ENV{TEST_AUTHOR}     : 1;
        $ENV{TEST_POD}        = defined $ENV{TEST_POD}        ? $ENV{TEST_POD}        : 1;
        $ENV{RELEASE_TESTING} = defined $ENV{RELEASE_TESTING} ? $ENV{RELEASE_TESTING} : 1;
    }

      $level eq 'test'      ? test($opt)
    : $level eq 'testup'    ? testup($opt)
    : $level eq 'testcover' ? testcover($opt)
    : $level eq 'install'   ? install($opt)
    : $level eq 'dist'      ? dist($opt)
    : $level eq 'release'   ? release($opt)
    : $level eq 'manifest'  ? manifest($opt)
    : !$level               ? return
    : $level eq 'list'      ? print join "\t", qw/test testup testcover install dist release manifest/, "\n"
    :                         die "No such command '$level'\n";

    return;
}

sub test {
    if ($opt->brew) {
        warn "TODO Implement the perlbrew code to run the tests";
    }

    return !( builder('test') );
}

sub testup {
    my $dir      = path('lib');
    my @children = $dir->children;

    while ( my $child = shift @children ) {
        if ( -d $child ) {
            push @children, $child->children;
        }
        elsif ( $child =~ /[.]p[ml]$/xms ) {
            my $libs = $opt->lib
                ? ' -I' . join ' -I', @{$opt->lib}
                : '';
            my $ans = system $opt->perl . " -Ilib -It/lib$libs -c $child";
            return 0 if $ans;
        }
    }

    return test();
}

sub testcover {
    return !( builder('testcover') );
}

sub install {
    if ( !test($opt) ) {
        return if !$opt->force;
        $opt->force($opt->force - 1);
    }

    # Check that there are no vim files in bin directories
    if (-d 'bin' && (my @vims = glob 'bin/.*.sw[poxmn]')) {
        warn "There are vim swap files in the bin dir:\n" . ( join "\n", @vims ) . "\n";
        return if !$opt->force;
        $opt->force($opt->force - 1);
    }

    system $opt->install . builder() . ' install';
}

sub dist {
    if ($opt->do && $opt->force) {
        die "Won't --do with --force!\n";
    }

    if ( !test($opt) ) {
        return if !$opt->force;
        $opt->force($opt->force - 1);
    }

    # Check that there are no vim files in bin directories
    if (-d 'bin' && (my @vims = glob 'bin/.*.sw[poxmn]')) {
        warn "There are vim swap files in the bin dir:\n" . ( join "\n", @vims ) . "\n";
        return if !$opt->force;
        $opt->force($opt->force - 1);
    }

    builder('dist');

    my @tars = sort glob '*.tar.gz';

    if (
        @tars
        && $tars[-1]
        =~ /^ ( [0-9a-zA-Z-]+ ) - v? ( [\d.]+ ) [.] tar [.] gz $/xms
    ) {
        my $dist = $1;
        my $ver  = $2;
        if ($ARGV[1] && $ARGV[1] =~ /^$ver/) {
            $ver = $ARGV[1];
        }

        ## Check t/00-load.t tests all modules
        my $modules = `find lib | grep '[.]pm\$' | wc -l`;
        my $tested  = `grep use_ok t/00-load.t | wc -l`;
        chomp $modules;
        chomp $tested;
        if ( $modules ne $tested ) {
            my @modules;
            for my $lib (`find lib | grep '[.]pm\$'`) {
                $lib =~ s{lib/|[.]pm\n}{}g;
                $lib =~ s{/}{::}g;
                push @modules, "use_ok('$lib');\n";
            }

            unlink $tars[-1];
            warn "t/00-load.t doesn't appear to test all found modules!\n";
            warn join '', @modules;
            return if !$opt->force;
            $opt->force($opt->force - 1);
        }

        ## Check t/boilerplate.t tests all modules
        my $files = -d 'bin' ? `find bin -type f | grep -Pv '[.]sw.\$' | wc -l` : 0;
        $modules = `find lib | grep -P '[.]p(m|od)\$' | wc -l`;
        $tested = `grep 'module_boilerplate_ok[(]' t/boilerplate.t | wc -l`;
        chomp $files;
        chomp $modules;
        chomp $tested;
        if ( $modules + $files != $tested ) {
            warn "Found $modules modules and $files files but only $tested tests\n";
            my @modules;
            my $dirs = -d 'bin' ? 'lib bin' : 'lib';
            for my $file (`find $dirs -type f | grep -Pv '[.]sw.\$'`) {
                chomp $file;
                push @modules, "module_boilerplate_ok('$file');\n";
            }

            unlink $tars[-1];
            warn "t/boilerplate.t doesn't appear to test all found modules and scripts!\n";
            warn join '', sort @modules;
            return if !$opt->force;
            $opt->force($opt->force - 1);
        }

        my $vcs = VCS::Which->new(dir => '.');

        # check that the code is all checked in
        if ( !$vcs->uptodate('.') ) {
            unlink $tars[-1];
            warn `pwd`;
            warn "There appear to be uncommitted changes in the directory\n";
            return if !$opt->force;
            $opt->force($opt->force - 1);
        }

        my $changes = path('Changes');

        die "Could not determine the current version of package!\n"
            if !defined $ver;

        if ( !-f $changes ) {
            my $date = strftime('%Y-%m-%d', localtime);
            path($changes)->spew(<<"CHANGES");
Revision history for $dist

$ver    $date
    Initial Release
CHANGES
            warn "Created Changes file, you may want to check its details.\n";
        }

        $changes = $changes->slurp;

        # check that the Changes file is up to date
        if ( $changes !~ /^v?$ver\s/xms ) {
            unlink $tars[-1];

            if ($opt->verbose) {
                my ($version, $date)
                    = $changes =~ /^v?( \d+ [.] \d+ (?: [.] \d+ )? ) \s+ (\d\d\d\d-\d\d-\d\d)/xms;

                if ( $date ) {
                    warn "git log --after=$date\n";
                    print $vcs->log("--after=$date");
                }
                else {
                    warn "Could not extract the date from the change log file!\n";
                }
            }

            die
                "There is no entry in the Changes file for this version! (using $ver)\n";
        }

        my $used_error = system 'used -q';
        if ( $used_error ) {
            warn "Build.pl not up to date!\ntry:\n    used -U\n";

            $opt->force(0) if !$opt->force;
            if ( $opt->force <= 0 ) {
                $opt->force($opt->force - 1);
                unlink $tars[-1];
                exit;
            }
        }

        # check for .travis.yml being up to date for perl versions
        my %versions_available = map { /(5[.][123][24680])/; $1 ? ($1 => 1) : () }
            `perlbrew available`;
        if ( ! %versions_available ) {
            # fallback if perlbrew isn't installed or doesn't return anything
            %versions_available = map { "5." . (2 * $_) . '.0' => 1 } 6 .. 14;
        }

        my $travis = LoadFile('.travis.yml');
        my %versions_specified = map {$_ => 1} @{ $travis->{perl} };

        my $meta = LoadFile('META.yml');
        my $min_version = $meta->{requires}{perl} || 5.10;
        $min_version =~ s/^v?(5[.]\d+).*$/$1/;

        for my $available (sort keys %versions_available) {
            my ($ava_long, $ava_short) = ($available) x 2;
            $ava_long =~ s/^5.([123])/5.0$1/;
            $ava_long =~ s/^5.([68])/5.00$1/;
            $ava_long =~ s/^5.(\d+)[.].*$/5.$1/;

            $ava_short =~ s/^5.(\d+)[.].*$/5.$1/;
            next if $opt->travis_min && $ava_long * 1.0 < $opt->travis_min * 1.0;
            next if $opt->travis_max && $ava_long * 1.0 > $opt->travis_max * 1.0;

            if ( ! ( $versions_specified{$available} || $versions_specified{$ava_short} )
                && $ava_long >= $min_version ) {
                my $message = ".travis.yml doesn't have $available configured\n";
                if ( ! $opt->force ) {
                    die $message;
                }
                warn $message;
                $opt->force($opt->force - 1);
            }
        }

        # check that there is a tag for this version
        my $tags = $vcs->exec('tag');
        chomp $tags;

        # finialise commands
        my $tag    = "git tag -a -m 'Tagging version $ver' '$ver'";
        my $push   = "git push origin master --tags";
        my $upload = "cpan-upload-http -verbose $tars[-1]";
        my $dch    = "git dch --auto";

        if ( $opt->do && ( !$tags || $tags !~ /^$ver$/xms ) ) {
            for ($tag, $push) {
                my $error = system $_;
                if ($error) {
                    warn "'$_' returned " . ($error >> 8) . "\n";
                    return;
                }
            }
            my $pause = { Config::General->new("$ENV{HOME}/.pause")->getall };
            CPAN::Uploader->upload_file($tars[-1], $pause);

            system $dch if -d 'debian';

            # now install the just released code.
            system builder();
            system $opt->install . builder() . ' install';
        }
        elsif ( $tags && $tags !~ /$ver/ ) {
            unlink $tars[-1];
            die "There are no tags for this version!\nUse:\n"
                . "    $tag\n"
                . "    $push\n";
        }
        elsif (!$tags) {
            warn "No tags were found!\n\n"
                . "Use:\n"
                . "    $tag\n"
                . "    $push\n"
                . "    $upload\n";
        }
        else {
            print "Upload to CPAN\n"
                . "    $upload\n";

            if ( -d 'debian' ) {
                print "    $dch\n";
            }
        }
    }
    elsif ( $tars[-1] ) {
        warn "Could not determine module version from '$tars[-1]'!\n";
    }
    else {
        warn "Could not determine module version from\n";
    }

    return;
}

sub release {
    my ($opt) = @_;

    my $changes   = path('Changes');
    my $changelog = $changes->slurp;
    my ($version) = $changelog =~ /\r?\n\r?\n(\S+)\s+/;
    my ($old, $new) = @ARGV;

    if ( !$old && !$new ) {
        $old = $version;
        $old =~ s/^v//;
        $new = $old;
        $new =~ s/(\d+)$/sprintf '%0'.(length $1).'d', $1+1/egxms;
        while ( length $new < length $old ) {
            $new .= '0';
        }
        print "builder release $version $new\n";
        return if !$opt->do;
    }
    elsif ( $new =~ /[.].*0$/ ) {
        my $message = "Don't create versions with 0 at the end, Module::Build doesn't like them!\n";
        if (!$opt->force) {
            die $message;
        }
        warn $message;
        $opt->force($opt->force - 1);
    }

    if ($opt->do && $opt->force) {
        die "Won't --do with --force!\n";
    }

    test($opt) || return if !$opt->do;

    my $old_re = $old;
    my $new_re = $new;
    $old_re =~ s/[.]/[.]/g;

    my $details = sha_from_show($old);
    my $old_date = DateTime->from_epoch( epoch => $details->{time} )->ymd('-');
    system "cs $old_re -r $new_re --yes -Nperl -Xbuild";
    my @all_log = `git log --format=format:'%s (%an) -%d' --since=$old_date`;
    my @log;
    my $log_message = "Updating for release of version $new";
    push @log, "       $log_message\n";
    for my $line (@all_log) {
        my ($log, $branches_tags) = $line =~ /^(.*) \s - \s (?: [(] (.*) [)] )? $/xms;
        my %branches_tags = map {$_ => 1} split /,\s/, $branches_tags || '';

        last if $branches_tags{$old} || $branches_tags{ version->new($old) };

        push @log, "       $log\n" if $log;
    }

    my $now = DateTime->now->ymd('-');
    $changelog =~ s/\r?\n(\r?\n(\S+)\s+)/\n\n$new   $now\n @log$1/;
    my $fh = $changes->openw;
    print {$fh} $changelog;

    if ( -d 'debian' ) {
        system "git dch --auto --release";
    }

    my $cmd = "git commit -am '$log_message'";
    print "$cmd'\n";
    print "$name -c dist\n";

    if ($opt->do) {
        system "vim Changes";
        system $cmd;
        builder('distclean');
        system $opt->perl . ' ' . build_pl();
        builder();
        test($opt);
        dist($opt);
    }
}

sub manifest {
    builder('manifest');
    unlink 'MANIFEST.bak' if -e 'MANIFEST.bak';
}

sub create_tags {
    my $tag = path('usr', 'bin', 'ctags');
    if ( $opt->tags && -x $tag ) {
        my @exclude = qw/blib _build Build tmp/;
        my $cmd = "$tag -R --exclude=" . join ' --exlude=', @exclude;
        warn "$cmd\n" if $opt->verbose || $opt->test;
        system $cmd if !$opt->test;
    }
}

sub sha_from_show {
    my ($name) = @_;
    my ($log) = qx/git rev-list -1 --timestamp $name/;
    confess "No commitish found for '$name'\n" if !$log;
    chomp $log;
    my ($time, $sha) = split /\s+/, $log;

    return {
        name     => $name,
        sha      => $sha,
        time     => $time,
    };
}

{
    my $builder;

    sub build {
        $builder = -e 'Build.PL' ? 'build' : 'make';
    }

    sub built {
        $builder || build();
        return
              $builder eq 'build' ? -e 'Build'
            : $builder eq 'make'  ? -e 'Makefile'
            :                       0;
    }

    sub builder {
        my ($action) = @_;

        my $cmd = $opt->lib
            ? 'PERL5LIB=$PERL5LIB:' . join ':', @{$opt->lib}
            : '';
        $cmd .=
              $builder eq 'build' ? ' ./Build'
            : $builder eq 'make'  ? ' make'
            :                       0;

        if ($builder eq 'build' && !-f 'META.json') {
            system "$cmd dist";
            copy 'MYMETA.json', 'META.json' if -e 'META.json';
            copy 'MYMETA.yml' , 'META.yml'  if -e 'META.yml';
        }

        return $action ? system "$cmd $action" : $cmd;
    }

    sub build_pl {
        return
              $builder eq 'build' ? 'Build.PL'
            : $builder eq 'make'  ? 'Makefile.PL'
            :                       0;
    }
}

__DATA__

=head1 NAME

builder - Builds perl packages to specified point

=head1 VERSION

This documentation refers to builder version 0.1.0.

=head1 SYNOPSIS

   builder (level) [--dist-clean]
   builder [option]
   builder test [-c|--dist-clean] [-a|--author-tests|--no-author] [(-j|--parallel) count]
   builder testcover [-c|--dist-clean] [-a|--author-tests|--no-author] [(-j|--parallel) count]
   builder install [-c|--dist-clean] [-a|--author-tests|--no-author]
   builder manifest
   builder release [old [new]] [-d|--do]
   builder dist [-d|--do]

 LEVELS:
  test      Just run the distribution's tests
  testup    Appears to check that libs compile
  testcover Run the distribution's test with code coverage
  install   Run the tests and if they pass install the distribution
  dist      Run the tests (with author tests) and if they pass create a new tar.gz for release
  release   Make changes for performing a CPAN release
  manifest  Update MANIFEST and MANIFEST.SKIP files with all the current files

 OPTIONS:
  -c --dist-clean
                Runs a dist clean at the start if the package has already been built
  -a --author-tests
                Turn on author tests, sets ENV TEST_AUTHOR to true (Default with dist)
     --no-author
                Turns off author tests (Default with all other levels)
  -d --do       When used with release it will perform version number changes and then
                kick of dist. When used with dist if everything is OK it will add the
                tag, push the code and send the code to PAUSE.
  -f --force    Force the continuation apon encountering an error
                (normally processin would stop). Use multiple times to
                get past multiple errors.
  -j --parallel-tests[=]int
                Set the number of parallel processes to use for running tests
  -i --install[=]str
                Command prefix when instlling (default sudo)
  -I --lib[=]string
                Set an other lib directories needed for testing
  -b --brew     When running tests with this option, the tests will also be
                run with the perl versions installed in perlbrew.
                Note: The module wont be build against those perl versions
                      so you will have to manually install dependencies for
                      the tested perl versions.
     --save     Save's current command line options into .builderrc file so the
                options don't need to be specified every time builder is run.
  -t --tags     Cause tags file to be generated for project
     --travis-min, --tmin[=]float
                Ignore missing perl versions from travis files below this version
     --travis-max, --tmax[=]float
                Ignore missing perl versions from travis files above this version

  -v --verbose  Show more detailed option
     --VERSION  Prints the version information
     --help     Prints this help information
     --man      Prints the full documentation for builder

=head1 DESCRIPTION

=head1 SUBROUTINES/METHODS

=head1 DIAGNOSTICS

=head1 CONFIGURATION AND ENVIRONMENT

Command line options can be written into a file C<.builderrc> so for a
specific repository they don't need to be specified all the time.

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

=head1 BUGS AND LIMITATIONS

There are no known bugs in this module.

Please report problems to Ivan Wills (ivan.wills@gmail.com).

Patches are welcome.

=head1 AUTHOR

Ivan Wills - (ivan.wills@gmail.com)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2009-2014 Ivan Wills (14 Mullion Close, Hornsby Heights, NSW Australia 2077).
All rights reserved.

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L<perlartistic>.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.

=cut
