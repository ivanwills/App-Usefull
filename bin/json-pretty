#!/usr/bin/perl

# Created on: 2015-06-29 11:40:18
# Create by:  Ivan Wills
# $Id$
# $Revision$, $HeadURL$, $Date$
# $Revision$, $Source$, $Date$

use strict;
use warnings;
use version;
use Getopt::Long;
use Pod::Usage;
use Data::Dumper qw/Dumper/;
use English qw/ -no_match_vars /;
use FindBin qw/$Bin/;
use Path::Tiny;
use JSON::XS;

our $VERSION = version->new('0.0.1');
my ($name)   = $PROGRAM_NAME =~ m{^.*/(.*?)$}mxs;
my $coder;

my %option = (
    indent  => '  ',
    verbose => 0,
    man     => 0,
    help    => 0,
    VERSION => 0,
);

main();
exit 0;

sub main {
    Getopt::Long::Configure('bundling');
    GetOptions(
        \%option,
        'depth|d=i',
        'verbose|v+',
        'man',
        'help',
        'VERSION!',
    ) or pod2usage(2);

    if ( $option{'VERSION'} ) {
        print "$name Version = $VERSION\n";
        exit 1;
    }
    elsif ( $option{'man'} ) {
        pod2usage( -verbose => 2 );
    }
    elsif ( $option{'help'} ) {
        pod2usage( -verbose => 1 );
    }

    # do stuff here
    my $json = @ARGV ? decode_json(scalar path($ARGV[0])->slurp) : decode_json(join '', <ARGV>);
    $coder = JSON::XS->new->ascii->pretty->allow_nonref;

    print out($json), "\n";

    return;
}

sub out {
    my ($json, $indent, $depth) = @_;
    my $out = '';
    $indent ||= '';
    $depth ||= 0;

    return '...' if $option{depth} && $depth >= $option{depth};

    if ( !ref $json ) {
        $out = $coder->encode($json);
    }
    elsif (ref $json eq 'HASH') {
        $out .= "{\n";
        for my $key ( sort {lc $a cmp lc $b } keys %$json ) {
            $out .= qq/$indent$option{indent}"$key": / . out($json->{$key}, "$indent$option{indent}", $depth + 1) . ",\n";
        }
        chop $out;
        chop $out;
        $out .= "\n$indent}";
    }
    elsif (ref $json eq 'ARRAY') {
        if (@$json) {
            $out .= "[\n";
            for my $val (@$json) {
                $out .= qq/$indent$option{indent}/ . out($val, "$indent$option{indent}", $depth + 1) . ",\n";
            }
            chop $out;
            chop $out;
            $out .= "\n$indent]";
        }
        else {
            $out .= "[]";
        }
    }
    else {
        $out = $coder->encode($json);
    }

    chomp $out;

    return $out;
}

__DATA__

=head1 NAME

json-pretty - Pretty print and order JSON data

=head1 VERSION

This documentation refers to json-pretty version 0.0.1

=head1 SYNOPSIS

   json-pretty [option]

 OPTIONS:
  -d --depth[=]int  Show results to this maximum depth

  -v --verbose      Show more detailed option
     --version      Prints the version information
     --help         Prints this help information
     --man          Prints the full documentation for json-pretty

=head1 DESCRIPTION

Most JSON checkers/pretty printers don't guarentee (as per the JSON standard)
the order of the out putted JSON object fields, I<json-pretty> does it will
ASCII order the fileds names making it easy for JSON configs to be stored in
version controll systems. This can also be useful for humans to review the
returned structures.

=head1 DIAGNOSTICS

=head1 CONFIGURATION AND ENVIRONMENT

=head1 DEPENDENCIES

=head1 INCOMPATIBILITIES

=head1 BUGS AND LIMITATIONS

There are no known bugs in this module.

Please report problems to Ivan Wills (ivan.wills@gmail.com).

Patches are welcome.

=head1 AUTHOR

Ivan Wills - (ivan.wills@gmail.com)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2015 Ivan Wills (14 Mullion Close, Hornsby Heights, NSW Australia 2077).
All rights reserved.

This module is free software; you can redistribute it and/or modify it under
the same terms as Perl itself. See L<perlartistic>.  This program is
distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY;
without even the implied warranty of MERCHANTABILITY or FITNESS FOR A
PARTICULAR PURPOSE.

=cut
